buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'net.researchgate:gradle-release:2.0.2'
    }
}

apply plugin: 'net.researchgate.release'

subprojects {
    buildscript {
        repositories {
            mavenCentral()
            jcenter()
        }

        dependencies {
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
            classpath 'net.researchgate:gradle-release:2.0.2'
        }
    }

    apply plugin: 'java'
    apply plugin: 'project-report'


    apply from: '../config/gradle/static-analysis.gradle'
    apply from: '../config/gradle/maven-publish.gradle'

    group = 'io.bitnet'

    repositories {
        mavenCentral()
        jcenter()
    }

    test {
        reports.html.enabled = false
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    tasks.withType(Jar) {
        baseName "bitnet-java-${project.name}"
        from("$rootDir") {
            include "LICENSE"
        }
        manifest {
            attributes("Implementation-Title": "bitnet-java-${project.name}",
                    "Implementation-Version": version,
                    "Implementation-Vendor-Id": "io.bitnet",
                    'Built-By': System.getProperty('user.name'),
                    'Built-JDK': System.getProperty('java.version'),
                    'Build-Time': new Date().format("yyyy-MM-dd HH:mm"))
        }
    }
}

task updateSDKVersions() {
    doLast {
        file('quickstart-java7/build.gradle').text = file('quickstart-java7/build.gradle').text.replaceAll(/io.bitnet:bitnet-java-sdk:.*/, "io.bitnet:bitnet-java-sdk:${project.version}'")
        file('quickstart-java7/pom.xml').text = file('quickstart-java7/pom.xml').text.replaceAll(/bitnet-java-sdk.*\n.*/, "bitnet-java-sdk</artifactId>\n            <version>${project.version}</version>")
        file('README.md').text = file('README.md').text.replaceAll(/io.bitnet:bitnet-java-sdk:.*/, "io.bitnet:bitnet-java-sdk:${project.version}'")
        file('README.md').text = file('README.md').text.replaceAll(/bitnet-java-sdk<\/artifactId>.*\n.*/, "bitnet-java-sdk</artifactId>\n        <version>${project.version}</version>")

        file('bitnet-codegen/build.gradle').text = file('bitnet-codegen/build.gradle').text.replaceAll(/io.bitnet:bitnet-java-client:.*/, "io.bitnet:bitnet-java-client:${project.version}:sources'")
    }
}

updateVersion.dependsOn ':client:bintrayUpload'
updateVersion.dependsOn ':sdk:bintrayUpload'
updateVersion.dependsOn updateSDKVersions